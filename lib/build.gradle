plugins {
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

def gitUrl = "https://github.com/aballano/$project.name"

def pomConfig = {
    licenses {
        license {
            name 'Apache-2.0'
            url "$gitUrl/blob/master/LICENSE"
        }
    }
    developers {
        developer {
            id 'aballano'
            name 'Alberto Ballano'
        }
    }
    scm {
        url "$gitUrl"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            groupId "$group"
            artifactId "$project.name"
            version "$project.version"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Simple memoization extension function for Kotlin')
                root.appendNode('name', "$group:$project.name")
                root.appendNode('url', "$gitUrl")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = "$project.name"
        licenses = ['Apache-2.0']
        websiteUrl = "$gitUrl"
        issueTrackerUrl = "$gitUrl/issues"
        vcsUrl = "${gitUrl}.git"
        version {
            name = "$project.version"
            desc = "$project.version"
            released  = new Date()
            vcsTag = project.version
        }
    }
}

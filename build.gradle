buildscript {
    ext.kotlin_version = '1.4.10'

    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:kotlinx.benchmark.gradle:0.2.0-dev-20"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

group = 'com.aballano'

subprojects { project ->
    apply plugin: "kotlin"
    apply plugin: 'kotlinx.benchmark'
    apply plugin: "kotlin-allopen"

    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }

    sourceSets {
        benchmarks
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation "org.jetbrains.kotlinx:kotlinx.benchmark.runtime-jvm:0.2.0-dev-20"

        testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0"

        benchmarksImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0"
        benchmarksCompile sourceSets.main.output + sourceSets.main.runtimeClasspath
    }

    allOpen {
        annotation("org.openjdk.jmh.annotations.State")
    }

    benchmark {
        configurations {
            main {
                warmups = 20
                iterations = 10
                iterationTime = 3
                iterationTimeUnit = "s"
            }
            smoke {
                warmups = 5
                iterations = 3
                iterationTime = 500
                iterationTimeUnit = "ms"
            }
        }
        targets {
            register("benchmarks")
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}